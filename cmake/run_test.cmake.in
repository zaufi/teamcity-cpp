set(_rtr_expected_output "@_rtr_expected_output@")
set(_rtr_expected_error "@_rtr_expected_error@")
set(_rtr_flowid_expected_output "@_rtr_flowid_expected_output@")
set(_rtr_flowid_expected_error "@_rtr_flowid_expected_error@")

if(PRETEND_TEAMCITY)
    set(ENV{TEAMCITY_PROCESS_FLOW_ID} myFlowId)
    set(ENV{TEAMCITY_PROJECT_NAME} teamcity-cpp)
    set(_output_flavour ".flowid")
    set(_expected_output "${_rtr_flowid_expected_output}")
    set(_expected_error "${_rtr_flowid_expected_error}")
else()
    set(_expected_output "${_rtr_expected_output}")
    set(_expected_error "${_rtr_expected_error}")
endif()

execute_process(
    COMMAND "${CMAKE_CURRENT_LIST_DIR}/@_rtr_TARGET@"
    OUTPUT_FILE "@_rtr_TARGET@${_output_flavour}.output"
    ERROR_FILE "@_rtr_TARGET@${_output_flavour}.error"
  )

foreach(kind "output" "error")
    set(file_prefix "@_rtr_TARGET@${_output_flavour}.${kind}")

    file(READ "${file_prefix}" _out)
    string(REPLACE "@PROJECT_SOURCE_DIR@/" "" _out "${_out}")
    string(REGEX REPLACE "duration='[0-9]+'" "duration='0'" _out "${_out}")
    file(WRITE "${file_prefix}" "${_out}")

    if(UPDATE_EXPECTATIONS)
        message(STATUS "Moving '${file_prefix}' -> '${_expected_${kind}}'")
        file(RENAME "${file_prefix}" "${_expected_${kind}}")
    else()
        execute_process(
            COMMAND "@DIFF_EXECUTABLE@" -Nru "${_expected_${kind}}" "${file_prefix}"
            RESULT_VARIABLE _diff_exit_code
            OUTPUT_FILE "${file_prefix}.diff"
          )

        if(${_diff_exit_code} EQUAL 0)
            file(REMOVE "${file_prefix}.diff")
        else()
            file(READ "${file_prefix}.diff" _diff_output)
            message(FATAL_ERROR "Standard ${kind} comparison failed: ${file_prefix}, diff:\n${_diff_output}")
        endif()
    endif()
endforeach()
