# - Find TeamCity Cpp Boost package
#
# Search for TeamCity Boost UTF integration files and set the following variables
# (as recommended by CMake finder module guidelines):
#   TEAMCITY_CPP_BOOST_INSTALL_PREFIX - install prefix
#   TEAMCITY_CPP_BOOST_VERSION        - found package version
#
# Also, find_package() would define a result variable named after module name
# and `_FOUND` suffix, which is `teamcity-cpp-boost_FOUND`.
# (yeah! `-` is a valid symbol for CMake identifiers ;-)
#
# This module provides an object library so you may add integration sources
# to your unit tests executable like this:
#
#   add_executable(
#       your-unit-tests-binary
#       test.cpp
#       $<TARGET_OBJECTS:teamcity-cpp-boost>
#     )
#

@PACKAGE_INIT@

get_filename_component(TEAMCITY_CPP_BOOST_INSTALL_PREFIX "${CMAKE_CURRENT_LIST_DIR}/../" ABSOLUTE)

# Setup some version strings as recommended by CMake guidelines
set(TEAMCITY_CPP_BOOST_VERSION "@PROJECT_VERSION@")

if(NOT TARGET teamcity-cpp-boost)
    add_library(
        teamcity-cpp-boost OBJECT
        "${TEAMCITY_CPP_BOOST_INSTALL_PREFIX}/teamcity_boost.cpp"
        "${TEAMCITY_CPP_BOOST_INSTALL_PREFIX}/teamcity_messages.cpp"
      )
    message(STATUS "Found TeamCity C++ Boost UTF integration library: ${TEAMCITY_CPP_BOOST_VERSION}")
endif()
